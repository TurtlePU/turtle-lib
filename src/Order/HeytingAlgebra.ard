\import Order.Lattice
\import Order.PseudocomplementedLattice
\import Paths.Meta

\class HeytingAlgebra \extends PseudocomplementedLattice {
  | \infixr 5 --> : E -> E -> E
  | rel-eval {x y : E} : x ∧ (x --> y) <= y
  | rel-univ {x y r : E} : x ∧ r <= y -> r <= x --> y

  \default comp \as pcomp (x : E) => x --> bottom
  \default non-contradiction \as pcomp-non-contradiction (x : E) : x ∧ pcomp x = bottom =>
  <=-antisymmetric rel-eval bottom-univ
  \default comp-univ \as pcomp-univ {x p : E} : x ∧ p = bottom -> p <= pcomp x =>
  \lam xp=0 => rel-univ (rewrite xp=0 <=-refl)
} \where \class Distributive \extends HeytingAlgebra, Bounded.DistributiveLattice