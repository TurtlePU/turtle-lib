\import Algebra.Algebra
\import Algebra.Group
\import Algebra.Meta
\import Algebra.Module
\import Algebra.Module.LieAlgebra
\import Algebra.Module.LinearOperator
\import Algebra.Monoid
\import Algebra.Monoid.Action
\import Algebra.Pointed
\import Algebra.Ring
\import Meta
\import Paths
\import Paths.Meta

\record Derivation \extends LinearOperator {
  \override A : Algebra
  | leibnitz {a b : A} : func (a * b) = a * func b + func a * b

  \lemma func-ide : func 1 = 0 =>
    A.cancel-left (func 1) (rewriteEq (inv (leibnitz {\this} {1} {1}), ide-left {A} {1}) (inv zro-right))
} \where {
  \lemma liftEq {V : Algebra} {f g : Derivation V} (p : f = {LinearOperator V} g) : f = {Derivation V} g =>
    exts (\lam v => pmap (\lam (f : LinearOperator V) => f v) p)
}

\instance DerivationAbGroup (A : Algebra) : AbGroup (Derivation A)
  | zro => \new Derivation {
    | LinearOperator => zro {LinearRing A}
    | leibnitz => rewrite (A.zro_*-right, A.zro_*-left) (inv zro-left)
  }
  | + f g => \new Derivation {
    | LinearOperator => f + {LinearRing A} g
    | leibnitz => rewrite (leibnitz {f}, leibnitz {g}) equation
  }
  | zro-left => exts (\lam _ => zro-left)
  | +-assoc => exts (\lam _ => +-assoc)
  | negative f => \new Derivation {
    | LinearOperator => negative {LinearRing A} f
    | leibnitz => rewrite (leibnitz {f}) equation
  }
  | negative-left => exts (\lam _ => negative-left)
  | +-comm => exts (\lam _ => +-comm)

\instance DerivationAlgebra {R : CRing} (A : Algebra.Over R) : LieAlgebra (Derivation A)
  | AbGroup => DerivationAbGroup A
  | C => R
  | c* c f => \new Derivation {
    | LinearOperator => c c* {LinearAlgebra A} f
    | leibnitz => rewrite (leibnitz {f}, c*-rdistr, c*-*-right, c*-*-left) idp
  }
  | ide-c* => exts (\lam _ => ide-c*)
  | *-c* => exts (\lam _ => *-c*)
  | c*-rdistr => exts (\lam _ => c*-rdistr)
  | c*-ldistr => exts (\lam _ => c*-ldistr)
  | % f g => \new Derivation {
    | LinearOperator => f % {LinearAlgebra A} g
    | leibnitz => run {
      unfold,
      rewrite (leibnitz {g}, func-+ {f}, leibnitz {f}, leibnitz {f}),
      unfold,
      rewrite (leibnitz {f}, func-+ {g}, leibnitz {g}, leibnitz {g}),
      equation
    }
  }
  | %-linear-left => Derivation.liftEq %-linear-left
  | %-linear-right => Derivation.liftEq %-linear-right
  | %-alter => Derivation.liftEq %-alter
  | %-jacobi => Derivation.liftEq %-jacobi